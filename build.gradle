plugins {
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "org.springframework.boot" version "2.3.5.RELEASE"
    id 'jacoco'
}

allprojects {
    group 'com.epam'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8

    dependencies {
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.3.5.RELEASE'
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.3.5.RELEASE'
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.3.5.RELEASE'
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: '2.3.5.RELEASE'
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: '2.3.4.RELEASE'
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.3.5.RELEASE'
        implementation group: 'org.springframework.security.oauth.boot', name: 'spring-security-oauth2-autoconfigure', version: '2.1.2.RELEASE'

        implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
        implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.8.0'

        implementation 'org.mapstruct:mapstruct:1.4.1.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.1.Final'

        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: '5.3.5.RELEASE'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
        testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.5.15'
        testImplementation group: 'org.jacoco', name: 'org.jacoco.agent', version: '0.8.5'
        testImplementation group: 'org.jacoco', name: 'org.jacoco.ant', version: '0.8.5'

        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        'com/epam/esm/config/**',
                        'com/epam/esm/exception/**',
                        'com/epam/esm/util/**',
                        'com/epam/esm/advice/**',
                        'com/epam/esm/Application.class',
                ])
            }))
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                excludes = [
                        'com.epam.esm.model',
                        'com.epam.esm.persistence'
                ]
            }
        }
    }
}

task generateMergedReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)

    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/epam/esm/entity',
                    'com/epam/esm/dto',
                    'com/epam/esm/mapper',
                    'com/epam/esm/specification',
                    'com/epam/esm/security/jwt',
                    'com/epam/esm/config/**',
                    'com/epam/esm/exception/**',
                    'com/epam/esm/util/**',
                    'com/epam/esm/advice/**',
                    'com/epam/esm/Application.class',
            ])
        }))
    }

    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}
